apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    aaptOptions { cruncherEnabled = false }

    defaultConfig {
        applicationId "com.hms.features"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled true
    }

    flavorDimensions "xadaptor"
    productFlavors {
        xmsg {
            dimension "xadaptor"
            versionNameSuffix "-xmsg"
        }
        xmsgh {
            dimension "xadaptor"
            versionNameSuffix "-xmsgh"
        }
        xmsh {
            dimension "xadaptor"
            versionNameSuffix "-xmsh"
        }
    }

    sourceSets {
        main {
            java{
                srcDirs 'src/main/java'
            }
            res {
                srcDirs 'src/main/res'
            }
        }
        xmsh {
            java {
                srcDirs 'src/xmsh/java'
            }
            res {
                srcDirs 'src/xmsh/res'
            }
        }

        xmsg {
            java {
                srcDirs 'src/xmsg/java'
            }

            res {
                srcDirs 'src/xmsg/res'
            }
        }

        xmsgh {
            java {
                srcDirs 'src/xmsgh/java','src/xmsg/java','src/xmsh/java'
            }

            res {
                srcDirs 'src/xmsgh/res','src/xmsg/res','src/xmsh/res'
            }
        }
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(path: ':AnalyticsKit')
    implementation project(path: ':AvailableToAllLbraries')
    implementation project(path: ':MapKit')
    implementation project(path: ':AccountKit')
    implementation project(path: ':LocationKit')
    implementation project(path: ':PushKit')
    implementation project(path: ':Adskit')
    implementation project(path: ':ParnoramaKit')
    implementation project(path: ':ScanKit')
    implementation project(path: ':MLKit')
    api project(path: ':CrashService')
    implementation project(path: ':PlacesNearby')
    implementation project(path: ':LandmarkLocations')

    def room_version = "2.2.1"
    def dagger_version = "2.25.2"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'
    // androidx
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    // room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // design
    implementation 'com.google.android.material:material:1.0.0'
    // lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation "androidx.lifecycle:lifecycle-runtime:2.1.0"
    kapt "androidx.lifecycle:lifecycle-compiler:2.1.0"
    // dagger 2
    api "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

//    implementation 'com.huawei.agconnect:agconnect-crash:1.4.0.300'
//  implementation 'com.huawei.hms:hianalytics:5.0.1.300'

}

if (getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains("xmsh")){
    apply plugin: 'com.huawei.agconnect'
} else if(getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains("xmsgh")){
    apply plugin: 'com.huawei.agconnect'
    apply plugin: 'com.google.gms.google-services'
} else if(getGradle().getStartParameter().getTaskNames().toString().toLowerCase().contains("xmsg")){
    apply plugin: 'com.google.gms.google-services'
} else{
    apply plugin: 'com.huawei.agconnect'
    apply plugin: 'com.google.gms.google-services'
}
